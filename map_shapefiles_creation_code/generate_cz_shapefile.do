* generate_cz_shapefile.do: Merges 1990 County shapefile into a 1990 CZ shapefile

*** Version history:
* 2016-08-05, Michael Stepner
* 2014-01-31, Michael Stepner
* 2013-07-14, Michael Stepner
* 2013-07-05, Michael Stepner
* 2013-04-15, Shelby Lin

/*******************************

** DEPENDENCIES **
- mergepoly.ado, installed from SSC by running: ssc install mergepoly

** INPUT FILES ** 
- cw_cty_czone.dta
	Provided by David Dorn at http://www.cemfi.es/~dorn/data.htm -- file [E6]
- county1990_database_clean.dta & county1990_coords_clean.dta
	Based on source file "co99_d90_shp.zip", provided by U.S. Census Bureau at http://www.census.gov/geo/maps-data/data/cbf/cbf_counties.html
	Generated by "import_county1990_shapefile.do"

** OUTPUT FILES **
- cz_database_clean.dta
- cz_coords_clean.dta

*******************************/

global root "/Users/michael/Documents/git_repos/maptile_geo_templates"
global raw "$root/raw_data"
global out "$root/map_shapefiles"
global code "$root/map_shapefiles_creation_code"
cd "$code"

set more off


*** Step 1: Use Dorn County -> CZ crosswalk to merge CZ variable onto county database
use "$raw/cw_cty_czone.dta", clear
rename cty_fips county
rename czone cz
keep county cz

merge 1:m county using "$out/county1990_database_clean", assert(3) nogen
compress


*** Step 2: Merge county polygons into CZs

mergepoly id using "$out/county1990_coords_clean", ///
	coordinates("$out/cz_coords_clean.dta") ///
	by(cz) replace
save12 "$out/cz_database_clean.dta", replace

* Resave coords in Stata 12 format
use "$out/cz_coords_clean.dta", clear
save12 "$out/cz_coords_clean.dta", replace
